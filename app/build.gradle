plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdk 30

    defaultConfig {
        applicationId "com.criticaltechworks.topheadlines"
        minSdk 26
        targetSdk 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    def localProperties = new Properties()
    localProperties.load(new FileInputStream(rootProject.file("gradle.properties")))

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "NEWS_API_KEY", localProperties['newsApiKey']
            buildConfigField "String", "BASE_URL", "\"https://newsapi.org/v2/\""
        }
        debug {
            buildConfigField "String", "NEWS_API_KEY", localProperties['newsApiKey']
            buildConfigField "String", "BASE_URL", "\"https://newsapi.org/v2/\""
        }
    }

    flavorDimensions "source"
    productFlavors {
        bbcNews {
            dimension "source"
            buildConfigField "String", "NEWS_SOURCE_ID", "\"bbc-news\""
            buildConfigField "String", "NEWS_SOURCE", "\"BBC News\""
            applicationIdSuffix ".bbcNews"
            versionNameSuffix "-bbcNews"
        }
        abcNews {
            dimension "source"
            buildConfigField "String", "NEWS_SOURCE_ID", "\"abc-news\""
            buildConfigField "String", "NEWS_SOURCE", "\"ABC News\""
            applicationIdSuffix ".abcNews"
            versionNameSuffix "-abcNews"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    implementation 'androidx.test:core-ktx:1.4.0'
    testImplementation 'org.mockito:mockito-core:3.3.0'
    testImplementation 'org.mockito:mockito-inline:2.8.47'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'

    implementation 'com.google.android.material:material:1.4.0'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.8.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.20'
    implementation 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.1.1'
}